plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'org.jetbrains.dokka' /*version '1.8.20' */
}

android {
    namespace 'com.dsofttech.dprefs'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../dPrefsDocumentation/html"))
}

task cleanDokkaModuleDocs() {
    delete file("../dPrefsDocumentation/html")
}

task createDokkaModuleDocs(type: Exec) {
    commandLine('./../gradlew', 'dokkaHtml')
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../dPrefsDocumentation/html"))
    def dPrefsLogo = file("../DPrefs/src/main/java/com/dsofttech/dprefs/d_prefs_logo.svg")
    String dokkaBaseConfiguration = """
    {
      "customAssets": ["${dPrefsLogo}"],
      "footerMessage": "Made with ❤️ by D-Soft-Tech, (c) 2023",
      "separateInheritedMembers": false,
      "mergeImplicitExpectActualDeclarations": false
    }
    """
    pluginsMapConfiguration.set(
            // fully qualified plugin name to json configuration
            ["org.jetbrains.dokka.base.DokkaBase": dokkaBaseConfiguration]
    )
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // Encrypted Shared Prefs
    implementation "androidx.security:security-crypto-ktx:$encrypted_shared_prefs"
    // Dokka
    dokkaPlugin "org.jetbrains.dokka:android-documentation-plugin:$dokka_version"
    // Gson
    implementation "com.google.code.gson:gson:$gson_version"
    // truth library for testing
    testImplementation "com.google.truth:truth:$truth_library_version"
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.dsofttech.dprefs'
                artifactId = 'dPrefs'
                version = '1.0.0'
            }
        }
    }
}